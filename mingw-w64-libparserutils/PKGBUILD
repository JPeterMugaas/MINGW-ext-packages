# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=libparserutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.4
pkgrel=1
pkgdesc="Library for building efficient parsers (mingw-w64)"
arch=('any')
url='http://www.netsurf-browser.org/projects/libparserutils/'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-libiconv")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" 'perl')
options=('strip' 'staticlibs')
source=("http://download.netsurf-browser.org/libs/releases/${_realname}-$pkgver-src.tar.gz"
        CMakeLists.txt
        test-htonl-ntohl.patch)
sha256sums=('322bae61b30ccede3e305bf6eae2414920649775bc5ff1d1b688012a3c4947d8'
            'SKIP'
            'd937f894a8c122dbd1cadd3a0a957d9f16cdc48e30edbcbbd377ecbcde06724b')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  cp ../CMakeLists.txt .
  apply_patch_with_msg test-htonl-ntohl.patch
}

build() {
  cd "${srcdir}"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_{SHARED,STATIC}_LIBS=ON \
      ../${_realname}-${pkgver}

  make
}


check() {
  cd "${srcdir}"/build-${CARCH}
  make test || true
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  sed  -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libparserutils.pc

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
